# Worktree-level just dispatch plus the Rust workspace recipes.
#
# Domain-specific recipes live in sibling `.just` modules so other surfaces
# (hangar, swarm, ...) can slot in later without recipe-name collisions; the
# workspace recipes below came from ainb-tui/justfile when the Cargo workspace
# moved to the repository root.
#
# Usage:
#   just --list-submodules           # see every domain
#   just skill-manager --list        # see skill-manager's recipes
#   just skill-manager up --tier full
#
# Install: `brew install just` (or your platform's equivalent).

set shell := ["bash", "-cu"]

mod skill-manager 'skill-manager.just'

# Default - list everything (top-level + submodules).
default:
    @just --list

# Build the project
build:
    cargo build

# Build in release mode
build-release:
    cargo build --release

# Run the application
run *args:
    cargo run -- {{args}}

# Rebuild plugins + both binaries + restart the daemon, then run the TUI
#
# The complete dev launch (replaces `./scripts/build-plugins.sh && cargo run`):
#   1. build-plugins.sh restages every subprocess plugin (the hangar TUI is
#      one; `cargo run` alone never rebuilds it → you'd get a STALE plugin).
#   2. build ainb + ainb-hangar-daemon.
#   3. daemon restart: `daemon start` no-ops on a live pid, so a rebuild alone
#      leaves the OLD daemon serving the DB (the blank-board trap). Restarting
#      puts the fresh binary in charge; the health pane then shows no drift.
#   4. run the TUI. AINB_BIN is pinned so every self-spawn resolves to the fresh
#      binary regardless of a brew `ainb` on $PATH.
dev *args:
    #!/usr/bin/env bash
    set -euo pipefail
    ./scripts/build-plugins.sh
    cargo build -p ainb --bin ainb -p ainb-hangar-daemon
    AINB="$(pwd)/target/debug/ainb"
    export AINB_BIN="$AINB"
    "$AINB" hangar daemon restart
    "$AINB" hangar daemon status
    exec "$AINB" {{args}}

# This mirrors `just dev`: Fleet must connect to the freshly-built Hangar
# daemon, not an older installed binary, so runtime install restarts it first.
# Build and open the native macOS Fleet app.
fleet:
    #!/usr/bin/env bash
    set -euo pipefail
    cargo build -p ainb --bin ainb -p ainb-hangar-daemon
    AINB="$(pwd)/target/debug/ainb"
    export AINB_BIN="$AINB"
    "$AINB" fleet runtime install
    BUILD_DIR="${TMPDIR:-/tmp}/ainb-fleet-derived-data"
    xcodebuild -project apps/ainb-fleet-macos/AINBFleet.xcodeproj -scheme AINBFleet -configuration Debug -destination 'platform=macOS,arch=arm64' -derivedDataPath "$BUILD_DIR" build
    open "$BUILD_DIR/Build/Products/Debug/AINBFleet.app"

# Run tests
test:
    cargo test

# Run tests with output
test-verbose:
    cargo test -- --nocapture

# Run the LIVE, no-mock e2e tripwire against the REAL claude binary.
#
# Skips CLEAN + LOUD (exit 0) when there is no authenticated `claude` on PATH,
# so a skip never masquerades as a pass. When claude IS present it builds the
# `ainb` binary (the CLI verbs the tripwire drives) + the daemon, then runs the
# gated test single-threaded with full output (never truncated).
test-live:
    #!/usr/bin/env bash
    set -euo pipefail
    if ! command -v claude >/dev/null 2>&1; then
        echo "SKIPPED: no authenticated claude on PATH"
        exit 0
    fi
    if ! claude -p --model haiku -- "reply with the single word PONG" 2>/dev/null | grep -qi PONG; then
        echo "SKIPPED: no authenticated claude on PATH"
        exit 0
    fi
    cargo build -p ainb --bin ainb
    cargo test -p ainb-hangar-daemon --features live-e2e --test live_e2e live_dispatch_writes_nonce_artifact -- --nocapture --test-threads=1

# Run the LIVE remote-repo + source-branch e2e leg (REAL daemon + claude).
#
# Proves the 0042 dispatch chain: a file:// remote is cloned, the worktree is
# based on the chosen feature branch (sentinel asserted FIRST), the agent's
# nonce lands in that tree, and only then task=done is trusted. Skips CLEAN +
# LOUD without an authenticated claude. Mutation: LIVE_E2E_BREAK_SOURCE_BRANCH=1
# must turn it RED.
test-live-branch:
    #!/usr/bin/env bash
    set -euo pipefail
    if ! command -v claude >/dev/null 2>&1; then
        echo "SKIPPED: no authenticated claude on PATH"
        exit 0
    fi
    if ! claude -p --model haiku -- "reply with the single word PONG" 2>/dev/null | grep -qi PONG; then
        echo "SKIPPED: no authenticated claude on PATH"
        exit 0
    fi
    cargo build -p ainb --bin ainb
    cargo test -p ainb-hangar-daemon --features live-e2e --test live_e2e live_dispatch_remote_repo_source_branch -- --nocapture --test-threads=1

# Run the LIVE, no-mock e2e tripwire against the REAL codex binary.
#
# The codex leg of `test-live`: same law (assert the on-disk nonce artifact
# FIRST, task=done only as a cross-check), one provider over. Skips CLEAN + LOUD
# (exit 0) when there is no authenticated `codex` on PATH, via a bounded
# liveness probe (`codex exec -- "reply PONG"`). macOS has no `timeout`, so a
# background killer bounds it. The Rust test carries the same skip gate, so this
# recipe only fast-fails the build when codex is plainly absent.
test-live-codex:
    #!/usr/bin/env bash
    set -euo pipefail
    if ! command -v codex >/dev/null 2>&1; then
        echo "SKIPPED: no codex binary on PATH"
        exit 0
    fi
    probe=$(mktemp)
    codex exec --skip-git-repo-check -- "reply with the single word PONG" >"$probe" 2>/dev/null &
    pid=$!
    ( sleep 90; kill "$pid" 2>/dev/null || true ) &
    killer=$!
    wait "$pid" 2>/dev/null && rc=0 || rc=$?
    kill "$killer" 2>/dev/null || true
    if [ "$rc" -ne 0 ] || ! grep -qi PONG "$probe"; then
        echo "SKIPPED: codex on PATH is not authenticated / did not answer the liveness probe"
        rm -f "$probe"
        exit 0
    fi
    rm -f "$probe"
    cargo build -p ainb --bin ainb
    cargo test -p ainb-hangar-daemon --features live-e2e --test live_e2e live_dispatch_codex_writes_nonce_artifact -- --nocapture --test-threads=1

# Run BOTH live e2e legs (claude + codex); each self-skips CLEAN + LOUD.
test-live-all: test-live test-live-codex test-live-branch

# Format code
fmt:
    cargo fmt

# Check formatting
fmt-check:
    cargo fmt --check

# Run clippy
lint:
    cargo clippy -- -D warnings

# Fix clippy issues automatically where possible
lint-fix:
    cargo clippy --fix --allow-dirty --allow-staged

# Fail if a temporary debugging probe (REVERT-PROBE / DO-NOT-COMMIT) was left
# behind in the Rust sources. Cheap, runs first in `just check`.
check-no-probes:
    bash scripts/check-no-probes.sh

# Regenerate docs/tui/cli.md from the ainb binary (source of truth)
gen-cli-ref:
    cargo build -p ainb
    AINB_BIN=target/debug/ainb bash scripts/gen-cli-reference.sh

# Fail if docs/tui/cli.md has drifted from the binary
#
# `git ls-files --error-unmatch` FIRST: `git diff` ignores untracked files, so
# a generated page that was never `git add`ed makes the diff a silent no-op and
# the gate passes while nothing ships.
check-cli-ref: gen-cli-ref
    git ls-files --error-unmatch docs/tui/cli.md > /dev/null
    test -f docs/tui/cli.md
    git diff --exit-code -- docs/tui/cli.md

# Regenerate docs/man/ainb.1 (the ainb(1) man page) from the ainb binary
gen-man:
    cargo build -p ainb
    AINB_BIN=target/debug/ainb bash scripts/gen-man.sh

# Fail if docs/man/ainb.1 has drifted from the binary, or was never committed
# (see check-cli-ref for why the tracked-ness assertion comes first)
check-man: gen-man
    git ls-files --error-unmatch docs/man/ainb.1 > /dev/null
    test -f docs/man/ainb.1
    git diff --exit-code -- docs/man/ainb.1

# Render the man page locally exactly as `man ainb` will after install
man-preview: gen-man
    man docs/man/ainb.1

# Run the v2 conformance axes against every in-tree plugin BINARY and print
# the plugin x axis matrix. The synthetic-canary suite in
# crates/ainb-plugin-cts-v2/tests/axes.rs proves the runtime is conformant;
# this proves the plugins we actually ship are.
cts-matrix:
    cargo test -p ainb-plugin-cts-v2 --test real_plugin_axes -- --nocapture

# Every plugin contract in one shot: the synthetic axes, the per-plugin
# matrix, and the ainb-plugin-protocol wire-surface semver gate.
cts-contracts:
    cargo test -p ainb-plugin-cts-v2 -- --nocapture

# Check everything (format, lint, test)
check:
    just check-no-probes
    just fmt-check
    just lint
    just check-cli-ref
    just check-man
    just test

# Fix formatting and linting issues
fix:
    just fmt
    just lint-fix

# Clean build artifacts
clean:
    cargo clean

# Install development dependencies
setup:
    rustup component add rustfmt clippy

# Build + stage all bundled plugins (subprocess binaries) into dist/plugins/
# Required before running tripwire_real_data_in_tui or anything that
# discovers plugins from dist/. On macOS this also re-signs each staged
# binary so AMFI doesn't SIGKILL it at exec.
stage-plugins:
    ./scripts/build-plugins.sh

# Same but release profile.
stage-plugins-release:
    ./scripts/build-plugins.sh --release
    cargo install just

# Watch and rebuild on changes
watch:
    cargo watch -x check -x test -x run

# Generate documentation
docs:
    cargo doc --open

# Run benchmarks (if any)
bench:
    cargo bench

# Check dependency licenses
audit:
    cargo audit
